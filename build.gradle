buildscript {
  repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/jetbrains/intellij-plugin-service' }
    maven { url 'https://www.jetbrains.com/intellij-repository/snapshots' }
  }
  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
  }
}

plugins {
  id "org.jetbrains.intellij" version "0.4.21"
  id "org.jetbrains.grammarkit" version "2020.2.1"
  id "checkstyle"
  id 'org.asciidoctor.jvm.convert' version '2.2.0'
  // once windows classpaths get too long, use this dependency to make them shorter again
  id "com.github.ManifestClasspath" version "0.1.0-RELEASE"
  id 'net.ltgt.errorprone' version '0.8.1' apply false
}

checkstyle {
  toolVersion '8.30'
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'org.jetbrains.grammarkit'
apply plugin: 'java'
apply plugin: 'net.ltgt.errorprone'

import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jsoup.Jsoup

allprojects {
  repositories {
    mavenLocal()
    jcenter()
  }
}

intellij {
  version 'IC-2020.2.1'
  pluginName 'asciidoctor-intellij-plugin'
  updateSinceUntilBuild false
  plugins = [
    'PsiViewer:202-SNAPSHOT.3', // used for debugging
    'PlantUML integration:2.25.2', // used to test highlighting of plantuml diagrams
    'tanvd.grazi:202.6397.21', // used for spell and grammar checking checking
    'com.intellij.javafx:1.0.2',
    'org.intellij.intelliLang', //
    'java' // used to integrate into the build via AsciiDocTargetScopeProvider
  ]
}

asciidoctor {
  sourceDir file('.')
  sources {
    include 'CHANGELOG.adoc'
  }
  outputDir file('build/docs')
}

patchPluginXml {
  dependsOn asciidoctor
  version System.getenv('TRAVIS_TAG')
  changeNotes = {
    Jsoup.parse(file('build/docs/CHANGELOG.html').getText('UTF-8'))
      .select("#releasenotes").get(0).nextElementSibling().children()
      .subList(0, 20)
      .stream().map { e ->
      e.html()
        .replaceAll('\\(preview, available from GitHub releases\\)', '')
        .replaceAll('#([0-9]+)', '<a href="https://github.com/asciidoctor/asciidoctor-intellij-plugin/issues/$1">#$1</a>')
        // regex for GitHub user names from https://github.com/shinnn/github-username-regex
        .replaceAll('(?i)@([a-z\\d](?:[a-z\\d]|-(?=[a-z\\d])){0,38})', '<a href="https://github.com/$1">@$1</a>')
    }
      .collect().join("\n")
  }
  pluginDescription "${file('src/main/resources/META-INF/description.html').getText('UTF-8')}"
}

runIde {
  jvmArgs = ['-Xmx1024m','-XX:+UnlockDiagnosticVMOptions']

  // should not automatically reload plugin on change in IDE, as JRuby is not very good at this
  systemProperty('idea.auto.reload.plugins', 'false')

  systemProperty('ide.plugins.snapshot.on.unload.fail', 'true')
}

publishPlugin {
  username 'ahus1'
  token System.getenv('PLUGIN_REPO_TOKEN')
  channels 'eap'
}

test {
  testLogging {
    // this show the full exception on failed tests on travis
    exceptionFormat = 'full'
  }
}

dependencies {
  errorprone 'com.google.errorprone:error_prone_core:2.3.2'
  errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
  /* snakeyaml is s used by asciidoctorj-pdf, but is actually provided within jruby-stdlib
   * a snakeyaml version in the classpath takes precedence, but IntelliJ includes a version that is too old
   * therefore this plugin includes the same version of snakeyaml that is already included in jruby-stdlib
   * to prevent loading the older version from IntelliJ.
   * When a different version than jruby-stdlib 9.2.12.0 is used after upgrading asciidoctorj,
   * double check the snakeyaml version.
   * https://github.com/asciidoctor/asciidoctorj-pdf/issues/25
   */
  implementation 'org.yaml:snakeyaml:1.23'
  implementation 'org.asciidoctor:asciidoctorj:2.4.0'
  implementation 'commons-io:commons-io:2.4'
  implementation 'nl.jworks.markdown_to_asciidoc:markdown_to_asciidoc:1.1'
  implementation 'org.apache.commons:commons-text:1.8'

  // when updating the versions here, also update them in AsciiDocDownloaderUtil for dynamic download
  testImplementation 'org.asciidoctor:asciidoctorj-diagram:2.0.2'
  testImplementation 'org.asciidoctor:asciidoctorj-pdf:1.5.3'

  testImplementation 'junit:junit:4.12'
  testImplementation 'com.tngtech.archunit:archunit-junit4:0.10.2'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.6.1'
}

def genRoot = file('gen')

sourceSets {
  main {
    java.srcDirs project.files(genRoot)
    resources {
      exclude('META-INF/description.html')
    }
  }
}

grammarKit {
}

task showLinkToContributorGuide {
  println("\nFirst time contributing to this plugin? Have a look at the IntelliJ AsciiDoc Plugin contributor's guide for coders:\n" +
    "https://intellij-asciidoc-plugin.ahus1.de/docs/contributors-guide/contribute-as-a-coder.html\n")
}

task generateAsciidocLexer(type: GenerateLexer) {
  source = "src/main/java/org/asciidoc/intellij/lexer/asciidoc.flex"
  targetDir = "gen/org/asciidoc/intellij/lexer"
  targetClass = "_AsciiDocLexer"
  purgeOldFiles = true
}

compileJava {
  options.errorprone.excludedPaths = '.*_AsciiDocLexer.*'
  options.errorprone.error('StreamResourceLeak') // enforce errors where there would be warning in the standard only
  // will print link to contributor guide at the start of each build
  dependsOn showLinkToContributorGuide
  dependsOn generateAsciidocLexer
}

// ensure to print hint to contributor's guide on build failures again
gradle.taskGraph.afterTask { Task task, TaskState state ->
  if (state.failure) {
    println("\nA build step failed. Maybe the IntelliJ AsciiDoc Plugin contributor's guide can give a hint?\n" +
      "https://intellij-asciidoc-plugin.ahus1.de/docs/contributors-guide/contribute-as-a-coder.html\n")
  }
}
