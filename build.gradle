buildscript {
    repositories {
        jcenter()
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    }
}


plugins {
    // upgrading to 0.3.10 leads to problems with MathJAX as the new version of the plugin
    // will bundle all resources inside a JAR file and this seems to annoy MathJax. 
    id "org.jetbrains.intellij" version "0.1.10"
    id "org.jetbrains.grammarkit" version "2018.2.2"
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'org.jetbrains.grammarkit'
apply plugin: 'java'

import org.jetbrains.grammarkit.tasks.*

allprojects {
    repositories {
        jcenter()
    }
}

intellij {
    // we support IC-2017.2, but PsiViewer is not available for the older version, therefore se IC-2018.1 to run it
    version 'IC-2018.1'
    pluginName 'asciidoctor'
    updateSinceUntilBuild false

//    publish {
//        pluginId '7910'
//        username publishUsername
//        password publishPassword
//    }

//     Uncomment to test against Android Studio
//    intellij.alternativeIdePath = '/Applications/Android Studio.app'
    systemProperty 'idea.is.internal', 'false'
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'org.asciidoctor:asciidoctorj-diagram:1.5.16'
    compile 'org.asciidoctor:asciidoctorj:2.0.0'
    compile 'commons-io:commons-io:2.4'
    // compile 'org.jruby:jruby:9.2.7.0' // use same depdendency as asciidoctorj instead of our own
    compile 'org.pegdown:pegdown:1.4.2'
    compile 'org.parboiled:parboiled-core:1.1.6'
    testCompile 'junit:junit:4.12'
}

def genRoot = file('gen')

sourceSets {
  main {
    java.srcDirs project.files(genRoot)
  }
}

grammarKit {
  // version of IntelliJ patched JFlex (see bintray link below), Default is 1.7.0-1
  jflexRelease = '1.7.0-1'

  // tag or short commit hash of Grammar-Kit to use (see link below). Default is 2017.1.6
  grammarKitRelease = '6452fde524'
}

task generateAsciidocLexer(type: GenerateLexer) {
  source = "src/main/java/org/asciidoc/intellij/lexer/asciidoc.flex"
  targetDir = "gen/org/asciidoc/intellij/lexer"
  targetClass = "_AsciiDocLexer"
  purgeOldFiles = true
}

compileJava {
  dependsOn generateAsciidocLexer
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2'
    distributionUrl = distributionUrl.replace("bin", "all")
}

task checkUpdates(type: com.github.benmanes.gradle.versions.updates.DependencyUpdatesTask) {
    description = "check denpendency updates"
    group = "check"
}
